#!/usr/bin/hhvm
<?hh

// define some constants
define('MANGO_INSTALL_PATH', '/usr/lib/mango');
define('MANGO_BINARY_PATH', '/usr/bin/mango');

function printLn(string $line): void {
	printf("%s\n", $line);
}

function usage(string $message): void {
	printLn("   uninstall: $message");
	printLn("       usage: uninstall [-y|--yes]");
	printLn("");
	printLn("     options:");
	printLn("    -y|--yes: non-interactive mode");
	printLn("");
	exit(1);
}

function error(string $message): void {
	printLn("fatal error: $message");
	exit(1);
}

function removeDirectory(string $path): void {
	if (file_exists($path)) {
		$dir = scandir($path);
		foreach ($dir as $f) {
			if ($f != '.' && $f != '..') {
				$fullPath = "$path/$f";
				if (is_dir($fullPath)) {
					removeDirectory($fullPath);
				} else {
					unlink($fullPath);
				}
			}
		}
		rmdir($path);
	}
}

function proceed(string $message, bool $skip = false): bool {
	if (!$skip) {
		printf("%s [Y/n] ", $message);
		$stdin = fopen('php://stdin', 'r');
		$input = trim(fgets($stdin));
		if ($input == 'n') {
			return false;
		} else {
			return true;
		}
	} else {
		return true;
	}
}

// parse command line arguments
$NON_INTERACTIVE_MODE = false;
$opts = getopt('y', array('yes'));
foreach (array_keys($opts) as $opt) {
	if ($opt == 'y' || $opt == 'yes') {
		$NON_INTERACTIVE_MODE = true;
	} else {
		usage("Unknown argument: $opt");
	}
}

// begin uninstallation
if (!$NON_INTERACTIVE_MODE) {
	printLn("====================================================================");
	printLn("Mango Framework Uninstaller");
	printLn("====================================================================");
	printLn("This script will completely remove mango frameowrk and all installed"); 
	printLn("libraries from your system");
	printLn("");
}

if (proceed('Do you wish to proceed with the uninstallation?', $NON_INTERACTIVE_MODE)) {
	printLn("* Removing mango framework and libraries...");
	removeDirectory(MANGO_INSTALL_PATH);
	printLn("All done!");
} else {
	printLn("Aborted!");
}

